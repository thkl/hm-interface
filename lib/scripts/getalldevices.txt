!devices
string sDeviceId;string sChannelId;string sDpId;boolean df = true;
Write('{"devices":[');
    foreach(sDeviceId, root.Devices().EnumIDs()){
        object oDevice = dom.GetObject(sDeviceId);
        if(oDevice){
            if(df) {df = false;} else { Write(',');}
            Write('{');
            Write('"id": ' # sDeviceId # ',');
            Write('"name": "' # oDevice.Name().UriEncode() # '",');
            Write('"address": "' # oDevice.Address() # '",');
            Write('"type": "' # oDevice.HssType() # '",');
            Write('"interface": ' # oDevice.Interface() # ',');
            Write('"visible": ' # oDevice.Visible() # ',');
            Write('"inuse": ' # oDevice.Used() # ',');
            Write('"enabled": ' # oDevice.Enabled() # ',');
            Write('"access": ' # oDevice.UserAccessRights(iulOtherThanAdmin) # ',');
            Write('"readyconfig": ' # oDevice.ReadyConfig() # ',');
            Write('"readyconfigchannels": ' # oDevice.ReadyConfigChns() # ',');
            Write('"unerasable": ' # oDevice.Unerasable() # ',');
            Write('"channels": [');
            boolean bcf = true;
            foreach(sChannelId, oDevice.Channels().EnumIDs()) {
                object oChannel = dom.GetObject(sChannelId);
                if (oChannel) {
                if(bcf) {bcf = false;} else {Write(',');}Write('{');
                Write('"id": ' # sChannelId # ',');
                Write('"name": "' # oChannel.Name().UriEncode() # '",');
                Write('"intf": ' # oDevice.Interface() # ',');
                Write('"address": "' # oChannel.Address() # '",');
                Write('"type": "' # oChannel.HssType() # '",');
                Write('"channeltype": ' # oChannel.ChannelType() # ',');
                Write('"direction": "' # oChannel.ChnDirection() # '",');
                Write('"aes":'# oChannel.ChnAESActive() # ',');
                Write('"label": "' # oChannel.Label() # '",');

                Write('"functions": [');
                string idf;boolean tf = true;
                foreach(idf,oChannel.ChnFunction()){
                    if(tf){tf=false;} else { Write(',');}
                    Write('' # idf # '');
                }
                Write('],');
                Write('"rooms": [');
                string idf;boolean tf = true;
                foreach(idf,oChannel.ChnRoom()){
                    if(tf){tf=false;} else { Write(',');}
                    Write('' # idf # '');
                }
                Write('],');
                Write('"access": ' # oChannel.UserAccessRights(iulOtherThanAdmin) # '');
                Write(',');
                Write('"dp":[');
                boolean dcf = true;
                foreach(sDpId,oChannel.DPs().EnumIDs()){
                    object oDP = dom.GetObject(sDpId);
                    if (oDP) {
                        if(dcf) {dcf = false;} else {Write(',');}
                        Write('{');
                        Write('"id": ' # sDpId # ',');
                        Write('"name": "' # oDP.Name() # '",');
                        Write('"valuetype": ' # oDP.ValueType() # ',');
                        Write('"valuesubtype": ' # oDP.ValueSubType() # ',');
                        
                        if (oDP.ValueSubType()==istEnum) {
                            Write('"valuelist": "' # oDP.ValueList() # '",');
                        } else {
                            Write('"valuelist": "",');
                        }

                        if (oDP.ValueType() != ivtString) {
                            Write('"min": "' # oDP.ValueMin() # '",');
                            Write('"max": "' # oDP.ValueMax() # '",');
                            Write('"unit": "' # oDP.ValueUnit().UriEncode() # '",');
                        }
            
                        if (oDP.HSSID()) {
                          string hssid = oDP.HSSID();
                          Write('"hssid": "' # hssid # '",');
                        } 
                        Write('"operations": "' # oDP.Operations() # '"');
                        Write('}');
                    }
                }
                Write(']');
                Write('}');
                }
            }
        Write(']}');
    }
}
Write(']}');